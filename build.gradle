// -*- coding: utf-8; mode: groovy -*-

buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "com.diffplug.spotless:spotless-plugin-gradle:3.10.0"
		classpath "de.thetaphi:forbiddenapis:2.2"
		classpath "de.aaschmid:gradle-cpd-plugin:1.2"
	}
}

plugins {
	id 'java-library'
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id "com.jfrog.bintray" version "1.8.4"
	id 'maven-publish'
}

// basic plugins
apply plugin: 'io.spring.dependency-management'

// code quality plugins
apply plugin: "checkstyle"
apply plugin: "findbugs"
apply plugin: "pmd"
apply plugin: "cpd"
apply plugin: "jacoco"
apply plugin: "de.thetaphi.forbiddenapis"
apply plugin: "com.diffplug.gradle.spotless"

// code quality configuration
apply from: "${rootProject.projectDir}/gradle/quality/checkstyle.gradle"
apply from: "${rootProject.projectDir}/gradle/quality/findbugs.gradle"
apply from: "${rootProject.projectDir}/gradle/quality/pmd.gradle"
apply from: "${rootProject.projectDir}/gradle/quality/cpd.gradle"
apply from: "${rootProject.projectDir}/gradle/quality/jacoco.gradle"
apply from: "${rootProject.projectDir}/gradle/quality/forbiddenapis.gradle"
apply from: "${rootProject.projectDir}/gradle/quality/spotless.gradle"

// custom configuration
apply from: "${rootProject.projectDir}/gradle/version.gradle"
apply from: "${rootProject.projectDir}/gradle/resolveDependencies.gradle"
apply from: "${rootProject.projectDir}/gradle/sourceArtifact.gradle"
apply from: "${rootProject.projectDir}/gradle/bintray.gradle"


group = "jp.xet.springframework.data.mirage"
ext.artifactId = "spring-data-mirage"

defaultTasks "clean", "build"

// compiler
sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
	options.compilerArgs << "-Werror"
	options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-deprecation"
}

javadoc {
	failOnError = false
}

// ======== libraries ========
repositories {
	jcenter()
	mavenCentral()
	maven { url 'http://dl.bintray.com/ws2ten1/maven-release' }
}

configurations {
	testCompile.extendsFrom compileOnly
}

dependencies {
	api "org.springframework.data:spring-data-commons"
	api "com.miragesql:miragesql:$mirageVersion"
	api "org.ws2ten1:ws2ten1-chunks:1.0.0-rc2"
	api "org.ws2ten1:ws2ten1-repositories:1.0.0-rc2"
	implementation "org.slf4j:slf4j-api"
	implementation "com.google.guava:guava:$guavaVersion"
	compileOnly "org.springframework:spring-context"
	compileOnly "org.springframework:spring-jdbc"

	// tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompile "com.h2database:h2:$h2DatabaseVersion"
	testCompile "ch.qos.logback:logback-classic"

	pmd 'net.sourceforge.pmd:pmd-java:5.8.1'
	pmd 'org.ow2.asm:asm:6.1'
}


configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.name == "commons-logging") {
			// prefer "jcl-over-slf4j" over "commons-logging":
			details.useTarget "org.slf4j:jcl-over-slf4j:$slf4jVersion"
		}
		if (details.requested.name == "log4j") {
			// prefer "log4j-over-slf4j" over "log4j":
			details.useTarget "org.slf4j:log4j-over-slf4j:$slf4jVersion"
		}
	}
}

bootJar {
		enabled = false
}

jar {
		enabled = true
}

// ======== wrapper ========
task wrapper(type: Wrapper) {
	gradleVersion = "4.6"
}
