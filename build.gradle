// -*- coding: utf-8; mode: groovy -*-

buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url 'https://plugins.gradle.org/m2/' }
	}
	dependencies {
		classpath 'de.thetaphi:forbiddenapis:2.6'
		classpath 'de.aaschmid:gradle-cpd-plugin:2.0'
		classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:$spotbugsPluginVersion"
	}
}

plugins {
	id 'java-library'
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.8.4'
	id 'pmd'
	id 'com.diffplug.gradle.spotless' version '3.23.1'
}

// basic plugins
apply plugin: 'io.spring.dependency-management'

// code quality plugins
apply plugin: "checkstyle"
apply plugin: "com.github.spotbugs"
apply plugin: "cpd"
apply plugin: "jacoco"
apply plugin: "de.thetaphi.forbiddenapis"

// code quality configuration
apply from: "${rootProject.projectDir}/config/checkstyle.gradle"
apply from: "${rootProject.projectDir}/config/spotbugs.gradle"
apply from: "${rootProject.projectDir}/config/pmd.gradle"
apply from: "${rootProject.projectDir}/config/cpd.gradle"
apply from: "${rootProject.projectDir}/config/jacoco.gradle"
apply from: "${rootProject.projectDir}/config/forbiddenapis.gradle"
apply from: "${rootProject.projectDir}/config/spotless.gradle"

// custom configuration
apply from: "${rootProject.projectDir}/gradle/version.gradle"
apply from: "${rootProject.projectDir}/gradle/resolveDependencies.gradle"
apply from: "${rootProject.projectDir}/gradle/sourceArtifact.gradle"
apply from: "${rootProject.projectDir}/gradle/bintray.gradle"

group = "jp.xet.springframework.data.mirage"
ext.artifactId = "spring-data-mirage"

defaultTasks "clean", "build"

// compiler
sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
	options.compilerArgs << "-Werror"
	options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-deprecation"
}

javadoc {
	failOnError = false
}

repositories {
	jcenter()
	mavenCentral()
	maven { url 'http://dl.bintray.com/ws2ten1/maven-release' }
}

configurations {
	testCompile.extendsFrom compileOnly
}

dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

dependencies {
	api "com.miragesql:miragesql:2.1.0"
	api "com.miragesql:miragesql-integration:2.1.0"
	api "org.ws2ten1:ws2ten1-repositories:1.0.0-rc12"
	api "org.ws2ten1:ws2ten1-chunks:1.0.0-rc11"
	implementation "org.slf4j:slf4j-api"
	implementation "com.google.guava:guava:24.1-jre"
	implementation "org.springframework.data:spring-data-commons"
	compileOnly "org.springframework:spring-context"
	compileOnly "org.springframework:spring-jdbc"

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "com.h2database:h2"
	testImplementation "ch.qos.logback:logback-classic"

	pmd 'net.sourceforge.pmd:pmd-java:6.16.0'
	pmd 'org.ow2.asm:asm:7.1'
}

wrapper {
	gradleVersion = '5.2.1'
	distributionType = Wrapper.DistributionType.ALL
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
	reports {
		html {
			enabled = true
		}
		xml {
			enabled = false
		}
	}
}
